def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

ext.supportVersion = '27.0.1'
ext.retrofitVersion='2.1.0'
ext.chuckVersion='1.0.4'

def internal = dependencyGroup {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

def support = dependencyGroup {
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation "com.android.support:support-v4:$supportVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
}

def util = dependencyGroup {

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.github.bumptech.glide:glide:3.6.1'
    implementation 'com.orhanobut:logger:1.15'
    implementation('com.github.ajitsing:sherlock:1.0.1@aar') { //reports any crash that occurs in your application as a notification
        transitive = true
    }
    implementation 'com.github.bumptech.glide:glide:3.6.1'
    implementation 'com.github.ybq:Android-SpinKit:1.1.0'
}

def tests = dependencyGroup {
    //local unit
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.2'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.2'

    //runner and rules
    androidTestImplementation "com.android.support.test:runner:0.5"
    androidTestImplementation "com.android.support.test:rules:0.5"

    //UI
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.1"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:3.0.1"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:3.0.1"
}

def kotlin = dependencyGroup {
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}



dependencies {
    internal()
    support()
    util()
    tests()
    kotlin()
}